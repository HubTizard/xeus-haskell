{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e17722d4",
   "metadata": {},
   "source": [
    "Two numbers x and y are chosen such that 2 ≤ x ≤ y ≤ 99.\n",
    "Mr. S is told their sum and Mr. P is told their product. \n",
    "The following dialogue ensues: \n",
    "\n",
    "- Mr. P: I don’t know the numbers.\n",
    "- Mr. S: I knew you didn’t know. I don’t know either.\n",
    "- Mr. P: Now I know the numbers.\n",
    "- Mr S: Now I know them too.\n",
    "\n",
    "John McCarthy, Formalization of two puzzles involving knowledge, http://jmc.stanford.edu/articles/puzzles/puzzles.pdf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "f4ca5797",
   "metadata": {},
   "outputs": [],
   "source": [
    "-- solution https://gist.github.com/redhill42/3897314\n",
    "puzzle :: (Num t, Eq t, Enum t) => t -> t -> [(t, t)]\n",
    "puzzle low high = [(x,y) | x <- [low..high],\n",
    "                          y <- [x..high],\n",
    "                          let (s,p) = (x+y, x*y),\n",
    "                          p_dont_know p,\n",
    "                          s_knew_p_dont_know s,\n",
    "                          p_now_know p,\n",
    "                          s_now_know s]\n",
    "   where pairs = [(a,b) | a <- [low..high], b <- [a..high]]\n",
    "         sumOf s = filter (\\(a,b) -> a + b == s) pairs\n",
    "         prodOf p = filter (\\(a,b) -> a * b == p) pairs\n",
    "         unique [_] = True\n",
    "         unique  _  = False\n",
    "         p_dont_know = not . unique . prodOf\n",
    "         s_knew_p_dont_know = all (\\(a,b) -> p_dont_know (a*b)) . sumOf\n",
    "         p_now_know = unique . filter (\\(a,b) -> s_knew_p_dont_know (a+b)) . prodOf\n",
    "         s_now_know = unique . filter (\\(a,b) -> p_now_know (a*b)) . sumOf\n",
    "\n",
    "main :: IO ()\n",
    "-- main = putStrLn $ show $ head $ puzzle 2 99\n",
    "main = print (head $ puzzle 2 99)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "b9d482a4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(4,13)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "main"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "5d0da77e",
   "metadata": {},
   "outputs": [],
   "source": [
    "low = 2\n",
    "high = 99\n",
    "pairs = [(a,b) | a <- [low..high], b <- [a..high]]\n",
    "sumOf s = filter (\\(a,b) -> a + b == s) pairs\n",
    "prodOf p = filter (\\(a,b) -> a * b == p) pairs\n",
    "unique [_] = True\n",
    "unique  _  = False\n",
    "pDontKnow = not . unique . prodOf\n",
    "sKnewPDontKnow = all (\\(a,b) -> pDontKnow (a*b)) . sumOf\n",
    "pNowKnow = unique . filter (\\(a,b) -> sKnewPDontKnow (a+b)) . prodOf\n",
    "sNowKnow = unique . filter (\\(a,b) -> pNowKnow(a*b)) . sumOf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "f97e2adf",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[(2,2),(2,3),(2,4),(2,5),(2,6)]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "take 5 pairs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "5cab2c7a",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[(2,15),(3,14),(4,13),(5,12),(6,11),(7,10),(8,9)]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "sumOf 17"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "003c31cb",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[(2,26),(4,13)]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "prodOf 52"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "id": "61d153e1",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "pDontKnow 52\n",
    "-- why does p not know? (because there are two possibilities)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "id": "de550504",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "sKnewPDontKnow 17\n",
    "-- how does s know that p doesn't know? why does s also not know?\n",
    "-- because for all pairs in sumOf 17 the products are composite numbers \n",
    "-- with at least two factor pairs "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 24,
   "id": "de358de9",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[True,True,True,True,True,True,True]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "map (\\(a,b) -> pDontKnow (a*b)) (sumOf 17)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "id": "af34ad66",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "pNowKnow 52\n",
    "-- why the changed state of knowledge of p?\n",
    "-- s not knowing means that s still has an ambiguity about prodOf 52\n",
    "-- but p can resolve this as (2,26) is not in sumOf 17 "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "id": "c8c7132d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[False,True]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "map (\\(a,b) -> sKnewPDontKnow (a+b)) (prodOf 52)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "id": "31705a8e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "sNowKnow 17\n",
    "-- why the changes state of knowledge of s\n",
    "-- since s knows that p has been able to resolve the ambiquity it must be (4,13)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f104e1ee",
   "metadata": {},
   "source": [
    "Further Reading van Ditmarsch, H., Ruan, J., & Verbrugge, R. (2008). \"Sum and Product in Dynamic Epistemic Logic.\" Journal of Logic and Computation, Vol. 18, No. 4. Available via ResearchGate."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "9.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
